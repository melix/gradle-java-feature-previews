plugins {
    id 'java-library'
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

// If you're running Gradle with Java 14 and want to try
// records, the two following configuration blocks are enough
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs "--enable-preview"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "--enable-preview"
}

// Gradle also works currently with Java 15 ea+26, but if it didn't
// you could run it with 14 and fork compilation to use 15

providers.environmentVariable("JDK15")
        .forUseAtConfigurationTime()
        .map { new File(it) }
        .orNull?.with { javaHome ->
            println("Configuring your build to use JDK 15 from $javaHome")
            tasks.withType(Test).configureEach {
                executable = "${javaHome}/bin/java"
            }

            tasks.withType(JavaCompile).configureEach {
                options.fork = true
                options.forkOptions.javaHome = javaHome
                options.compilerArgs << "--release" << "15"
            }
        }

